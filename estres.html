<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8">

    <title>marialluïsa - estrès</title>
    <meta name="description" content="marialluïsa">
    <meta name="author" content="Robin Jiménez">
    <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <link rel="stylesheet" href="css/styles.css">

    <script src="https://code.jquery.com/jquery-3.4.1.slim.min.js"
            integrity="sha256-pasqAKBDmFT4eHoN2ndd6lN370kFiGUFyTiUHWhU7k8=" crossorigin="anonymous"></script>
</head>
<body class="estres">
<template>
    <a class="btn" id="back-button" href="index.html" disabled>torna enrere</a>
</template>
<main>
    <div class="content">
        <div id="output" class="output"></div>

        <div id="overlay">
            <div class="loading-message show">
                <p class="loading-info">Carregant</p>
            </div>
            <div class="orientation-message show">
                <p id="orientation-info">Posa el dispositiu en vertical i bloqueja l'orientació.</p>
            </div>
            <div class="overlay-message">
                <p class="main experience-info">
                    Activa el so. Puja el volum. Fes coses.
                </p>
                <p class="experience-info mobile">
                    Per gaudir de l'experiència només ens has de permetre
                    <wbr>
                    conèixer l'orientació del teu dispositiu.
                </p>
                <p class="experience-info mobile">
                    No rebrem ni s'emmagatzemarà cap dada.
                </p>
                <button class="text-button" id="start-button" title="D'acord" type="button">D'acord</button>
            </div>
        </div>

        <canvas id="display"></canvas>
    </div>
    <script id="custom-vertex" type="x-shader/x-vertex">

      varying vec3 v_uv;


      void main() {
        v_uv = position;

        vec4 modelViewPosition = modelViewMatrix * vec4(position, 1.0);
        gl_Position = projectionMatrix * modelViewPosition;
      }

    </script>
    <script id="custom-fragment" type="x-shader/x-fragment">

      /* https://www.shadertoy.com/view/XsX3zB
       *
       * The MIT License
       * Copyright © 2013 Nikita Miropolskiy
       */

      /* discontinuous pseudorandom uniformly distributed in [-0.5, +0.5]^3 */
      vec3 random3(vec3 c) {
          float j = 4096.0*sin(dot(c,vec3(17.0, 59.4, 15.0)));
          vec3 r;
          r.z = fract(512.0*j);
          j *= .125;
          r.x = fract(512.0*j);
          j *= .125;
          r.y = fract(512.0*j);
          return r-0.5;
      }

      /* skew constants for 3d simplex functions */
      const float F3 =  0.3333333;
      const float G3 =  0.1666667;

      /* 3d simplex noise */
      float simplex3d(vec3 p) {
           /* 1. find current tetrahedron T and it's four vertices */
           /* s, s+i1, s+i2, s+1.0 - absolute skewed (integer) coordinates of T vertices */
           /* x, x1, x2, x3 - unskewed coordinates of p relative to each of T vertices*/

           /* calculate s and x */
           vec3 s = floor(p + dot(p, vec3(F3)));
           vec3 x = p - s + dot(s, vec3(G3));

           /* calculate i1 and i2 */
           vec3 e = step(vec3(0.0), x - x.yzx);
           vec3 i1 = e*(1.0 - e.zxy);
           vec3 i2 = 1.0 - e.zxy*(1.0 - e);

           /* x1, x2, x3 */
           vec3 x1 = x - i1 + G3;
           vec3 x2 = x - i2 + 2.0*G3;
           vec3 x3 = x - 1.0 + 3.0*G3;

           /* 2. find four surflets and store them in d */
           vec4 w, d;

           /* calculate surflet weights */
           w.x = dot(x, x);
           w.y = dot(x1, x1);
           w.z = dot(x2, x2);
           w.w = dot(x3, x3);

           /* w fades from 0.6 at the center of the surflet to 0.0 at the margin */
           w = max(0.6 - w, 0.0);

           /* calculate surflet components */
           d.x = dot(random3(s), x);
           d.y = dot(random3(s + i1), x1);
           d.z = dot(random3(s + i2), x2);
           d.w = dot(random3(s + 1.0), x3);

           /* multiply d by w^4 */
           w *= w;
           w *= w;
           d *= w;

           /* 3. return the sum of the four surflets */
           return dot(d, vec4(52.0));
      }

      /* const matrices for 3d rotation */
      const mat3 rot1 = mat3(-0.37, 0.36, 0.85,-0.14,-0.93, 0.34,0.92, 0.01,0.4);
      const mat3 rot2 = mat3(-0.55,-0.39, 0.74, 0.33,-0.91,-0.24,0.77, 0.12,0.63);
      const mat3 rot3 = mat3(-0.71, 0.52,-0.47,-0.08,-0.72,-0.68,-0.7,-0.45,0.56);

      /* directional artifacts can be reduced by rotating each octave */
      float simplex3d_fractal(vec3 m) {
          return   0.5333333*simplex3d(m*rot1)
                  +0.2666667*simplex3d(2.0*m*rot2)
                  +0.1333333*simplex3d(4.0*m*rot3)
                  +0.0666667*simplex3d(8.0*m);
      }

      // Custom code

      uniform vec2 u_resolution;
      uniform float u_time;
      uniform float u_zTrans;

      uniform vec3 u_colorA;
      uniform vec3 u_colorB;
      uniform vec3 u_colorC;
      uniform vec3 u_colorD;

      uniform float u_opacity;
      uniform float u_depthDarkness;

      varying vec3 v_uv;

      void main() {
        vec2 st = v_uv.xy/u_resolution.xy;
        vec3 st3 = vec3(st, u_time*0.3*u_zTrans - v_uv.z/2000.0);

        vec3 color = vec3(0.0);
        float f1 = simplex3d_fractal(st3*8.0+5.0);
        f1 = 0.4 + 0.7*f1;
        float f2 = simplex3d(st3*5.0+1.0);
        f2 = 0.2 + 0.6*f2;
        float f3 = simplex3d_fractal(st3*2.0+8.0);
        f3 = 0.2 + 0.5*f3;

        color = mix(u_colorA,u_colorB,clamp((f1*f1)*4.0,0.0,1.0));

        color = mix(color,u_colorC,clamp(f2,0.0,1.0));

        color = mix(color,u_colorD,clamp(f3,0.0,1.0));

        //gl_FragColor = vec4((f1*f1*f1+0.6*f1*f1+0.5*f1)*color, u_opacity * (v_uv.z * 0.005 + 3.0));
        gl_FragColor = vec4((f1*f1*f1+0.6*f1*f1+0.5*f1) * color, u_opacity * (v_uv.z * 0.005 + 3.0));
      }
    </script>
</main>
<script src="lib/howler/dist/howler.js"></script>
<script src="lib/body-scroll-lock/lib/bodyScrollLock.js"></script>
<script src="lib/ammo.js"></script>
<script src="js/init.js"></script>
<script type="module" src="js/estres.js"></script>
</body>
</html>
